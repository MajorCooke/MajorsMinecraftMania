Class MCExplosion : MinecraftEntity
{
	meta bool Smokeless;
	meta bool Silent;
	Property Silent: Silent;
	Property Smokeless: Smokeless;
	Default
	{
		MCExplosion.Silent false;
		Radius 1;
		Height 1;
		Scale 256;
		Projectile;
		+NOINTERACTION
		+NOTONAUTOMAP
		+INVISIBLE
		+FORCERADIUSDMG
		RenderStyle "None";
	}
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay 
		{
			if (!Silent)	A_StartSound("Minecraft/Explode");
		}
		TNT1 A 10
		{
			
			int mm = int(ceil(Scale.X)); //Main explosion
			int nn = int(round(0.5 * Scale.X)); //
			for (int i = 0; i < mm; i++)
			{
				Class<Actor> spr = (i <= nn) ? 'MCExplosionSprite' : 'MCExplosionSpriteDelay';
				Angle = irandom(0,359);
				Pitch = ffrandom(-90, 90);
				double dist = ffrandom(0.1, 1.0) * Scale.X;
				
				A_SpawnItemEx(spr, cos(pitch) * dist, 0, -sin(pitch) * dist, flags: SXF_NOCHECKPOSITION);
				
				if (MCC_SFX < 1)	continue;
				if (!Smokeless && (i <= nn))
				{
					int rngcap = (MCC_SFX == 1) ? 4 : 2;
					if (irandom(1, rngcap) != 1)	continue;
					
					double velo = frandom(2.0, 4.0);
					Vector2 AP = (frandom(0,359),frandom(-90,90));
					A_SpawnItemEx("MCExplosionSmoke", 
						cos(pitch) * dist, 0, -sin(pitch) * dist, 
						cos(AP.Y) * velo, 0, -sin(AP.Y) * velo, 
						AP.X, SXF_NOCHECKPOSITION);
				}		
				/*
				if (!Smokeless && i < nn)
				{
					double velo = frandom(2.0, 4.0);
					Vector2 AP = (random(0,359),random(-90,90));
					A_SpawnItemEx("MCExplosionSmoke", 
						cos(AP.Y) * dist, 0, -sin(AP.Y) * dist,
						cos(AP.Y) * velo, 0, -sin(AP.Y) * velo, 
						AP.X, SXF_NOCHECKPOSITION, 64);
				}
				*/
				
			}
		}
		Stop;
	}
}

Class MCExplosionSmokeless : MCExplosion
{
	Default
	{
		MCExplosion.Smokeless true;
	}
}

Class MCExplosionSprite : Actor
{
	mixin TM_FastRNG;
	int DelayMin, DelayMax;
	Property Delay: DelayMin, DelayMax;
	Default
	{
		MCExplosionSprite.Delay 0, 0;
		+NOINTERACTION
		+NOBLOCKMAP
		+ROLLSPRITE
		+BRIGHT
		Scale 3.5;
	}
	
	States
	{
	Spawn:
		TNT1 A 1 NoDelay 
		{
			roll = randompick(0, 90, 180, 270);
			A_SetScale(Scale.X + ffrandom(-0.35, 0.35));
			static const Name Trans[] = 
			{
				'MCExplo1',
				'MCExplo2',
				'MCExplo3',
				'MCExplo4',
				'MCExplo5'
			};
			int size = Trans.Size();
			int i = irandom(0, size);
			if (i < size)
				A_SetTranslation(Trans[i]);
			A_SetTics(Max(0, irandom(DelayMin, DelayMax)));
		}
		X208 ABCDEFGHIJKLMNOP 1;
		Stop;
	}
}

Class MCExplosionSpriteDelay : MCExplosionSprite
{
	Default
	{
		MCExplosionSprite.Delay 1, 10;
	}
}

Class MCExplosionSmoke : Actor
{
	mixin TM_FastRNG;
	Default
	{
		+NOINTERACTION
		+NOBLOCKMAP
		Scale 4.0;
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		roll = randompick(0, 90, 180, 270);
	}
	
	override void Tick()
	{
		if (IsFrozen())	return;
		
		Vel.XY *= 0.98;
		if (Vel.Z < 1.0)
			Vel.Z += 0.05;
		Super.Tick();
	}
	
	States
	{
	Spawn:
		X208 QRSTUVWX random(10, 30);
		Stop;
	}
}