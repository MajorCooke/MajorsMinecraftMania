/*

Minecraft/Spider/Death
Minecraft/Spider/Idle
Minecraft/Spider/Step

-Legend-	X		Y		Z		Roll	Pitch	Angle
Back:		-0.6	0.0		2.0	
Head:		0.6		0.0		2.0			
Leg:		0.0		~.8		1.9		0.0		35		(Starts at 30, +40 each)

- Gibs -
Back:		-1.8	0.0		2.0		
Head:		1.4		0.0		2.0
Leg:		0.0		~2.2	1.9

- Both -
Thorax:		0.0		0.0		2.0

Frames
	A: Normal Spider
	B: Cave Spider

*/

	
		
Class MCSpider : MinecraftEnemy
{
	const _Radius = 1.6 + 1.2 + 2.4;
	const _Height = 2.0;
	const RealScale = 9.0;
	const RealRadius = RealScale * _Radius;
	const RealHeight = RealScale * _Height;
	const _MeleeRange = RealRadius + 8;
	enum ELimbs
	{
		Head = 0,
		Back,
		Thorax,
		LegL1,
		LegL2,
		LegL3,
		LegL4,
		LegR1,
		LegR2,
		LegR3,
		LegR4,
		LimbCount
	};
	enum EType
	{
		S_Normal = 0,
		S_Cave
	};
	
	double LeapRange;
	int Type;
	
	Property Type: Type;
	Property LeapRange: LeapRange;
	
	Default
	{
		Health 100;
		MinecraftEnemy.MislChance 100;
		MinecraftEnemy.StepSound "Minecraft/Spider/Step", 20;
		MCSpider.LeapRange _MeleeRange * 2;
		MCSpider.Type S_Normal;
		Radius RealRadius;
		Height RealHeight;
		Scale RealScale;
		ActiveSound "Minecraft/Spider/Idle";
		PainSound "Minecraft/Spider/Idle";
		DeathSound "Minecraft/Spider/Death";
		Tag "Spider";
		Obituary "%o's became a living host for spider breeding.";
		
		MeleeRange _MeleeRange;
		MaxTargetRange 768;
		Speed 6;
	}
	
	override bool DropItems(Actor src, Actor inf, Name mod)
	{
		if (!Super.DropItems(src, inf, mod))
			return false;
		
		int end = 5;
		int c[2];
		bool bp = false;
		for (int i = 0; i < end; i++)
		{
			int rng = irandom(0,10);
			if (rng == 0 && !bp)		
			{
				bp = true;
				Event.DropItem(self, 'Backpack');
			}
			else if (rng <= 5)	c[0]++;
			else				c[1]++;
		}
		Event.DropItem(self, 'HealthBonus', c[0]);
		Event.DropItem(self, 'ArmorBonus', c[1]);
		return true;
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		Init(LimbCount);
		Limb[Head] = 	MCBodyPart(Spawn('MCSpiderHead', pos));
		Limb[Back] = 	MCBodyPart(Spawn('MCSpiderBack', pos));
		Limb[Thorax] =	MCBodyPart(Spawn('MCSpiderThorax', pos));
		Limb[LegL1] =	MCBodyPart(Spawn('MCSpiderLeg', pos));
		Limb[LegL2] =	MCBodyPart(Spawn('MCSpiderLeg', pos));
		Limb[LegL3] =	MCBodyPart(Spawn('MCSpiderLeg', pos));
		Limb[LegL4] =	MCBodyPart(Spawn('MCSpiderLeg', pos));
		Limb[LegR1] =	MCBodyPart(Spawn('MCSpiderLeg', pos));
		Limb[LegR2] =	MCBodyPart(Spawn('MCSpiderLeg', pos));
		Limb[LegR3] =	MCBodyPart(Spawn('MCSpiderLeg', pos));
		Limb[LegR4] =	MCBodyPart(Spawn('MCSpiderLeg', pos));
		for (int i = 0; i < Limb.Size(); i++)
		{
			if (Limb[i])
			{
				Limb[i].Master = self;
				Limb[i].Owner = self;
				Limb[i].Scale = Scale;
				Limb[i].Frame = Type;
			}
		}
		
		Limb[Head].SetPosOffsets((		 0.6,	 0.0, 2.0));
		Limb[Back].SetPosOffsets((		-0.6,	 0.0, 2.0));
		Limb[Thorax].SetPosOffsets((	 0.0,	 0.0, 2.0));
		Limb[LegL1].SetPosOffsets((		 0.0,	-0.8, 1.9));
		Limb[LegL2].SetPosOffsets((		 0.0, 	-0.8, 1.9));
		Limb[LegL3].SetPosOffsets((		 0.0,	-0.8, 1.9));
		Limb[LegL4].SetPosOffsets((		 0.0,	-0.8, 1.9));
		Limb[LegR1].SetPosOffsets((		 0.0,	 0.8, 1.9));
		Limb[LegR2].SetPosOffsets((		 0.0,  	 0.8, 1.9));
		Limb[LegR3].SetPosOffsets((		 0.0, 	 0.8, 1.9));
		Limb[LegR4].SetPosOffsets((		 0.0, 	 0.8, 1.9));
		
		
		Limb[LegL2].SetSineOffset((180,0,0));		
		Limb[LegL4].SetSineOffset((180,0,0));
		Limb[LegR1].SetSineOffset((180,0,0));
		Limb[LegR3].SetSineOffset((180,0,0));
		
		
		int t = 0;
		for (int i = LegL1; i < LegL1 + 4; i++)
		{
			Limb[i  ].SetPersistantAPROffset(( 30 + (t * 40), 35, 0));
			Limb[i+4].SetPersistantAPROffset((-30 - (t * 40), 35, 0));
			t++;
		}
		UpdateLimbs();
	}
	
	override bool CanCollideWith(Actor other, bool passive)
	{
		if (other && other.bMISSILE)
			return true;
		if (!other || (other.bSOLID && !other.bACTLIKEBRIDGE))
			return false;
		return true;
	}
	
	override void DamageReact(Actor inf, Actor src, int damage, int dmg, Name mod, int flags, double angle, bool secdmg)
	{
		Super.DamageReact(inf, src, damage, dmg, mod, flags, angle, secdmg);
		
		if (!secdmg && !(flags & DMG_THRUSTLESS) && LeapTimer < Second)
		{
			LeapTimer = Second;
		}
	}
	
	private int LeapTimer;
	
	override void Tick()
	{
		Super.Tick();
		
		if (bKILLED || IsFrozen())
			return;
			
		if (LeapTimer > 0)	LeapTimer--;
	}
	
	States
	{
	Spawn:
		TNT1 AA 35;
		Goto IdleMove;
	Idle:
		TNT1 A 0 A_Jump(256, random(1, 4));
		TNT1 AAAAAAAAAA 17 Look();
	IdleMove:
		TNT1 A 0 
		{
			LookTimer = 0;
			return A_Jump(256, random(1, 35*2));
		}
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 LookWander();
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 LookWander();
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 LookWander();
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 LookWander();
		Goto Idle;
	See:
		TNT1 A 1
		{
			if (LeapTimer < 1 &&
				CheckTrace(Target, LeapRange) && Target != Goal &&
				Target.Pos.Z + Target.Height >= Pos.Z && 
				Target.Pos.Z <= Pos.Z + 32 &&
				OnGround)
			{
				LeapTimer = Second;
				if (MCC_Debug == 8)
					Console.Printf("%.1f", VelPosLen);
				Vel3DFromAngle(VelPosLen + 4, angle, 0);
				Vel.Z += 8.0;
				OnGround = false;
			}
		//	int flag = (OnGround) ? 0 : CHF_DONTMOVE;
		//	Chase(flags: flag, _3dmelee: true);
			Chase(_3dmelee: true);
		}
		Wait;
	Melee:
		TNT1 A 1 DoMelee(irandom(20, 25));
		Goto See;
	}
}

Class MCCaveSpider : MCSpider
{
	Default
	{
		MinecraftEnemy.OverScale 0.5;
		MCSpider.Type S_Cave;
		Tag "Cave Spider";
		Obituary "%o couldn't fight off the swarming Cave Spiders.";
	}
}

//------------------------------------------------------------------------------
// Body parts
Class MCSpiderBack : MCBodyPart {}
Class MCSpiderHead : MCBodyPartHead {}
Class MCSpiderThorax : MCBodyPart	{}

Class MCSpiderLeg : MCBodyPartLeg	
{
	Default
	{
		MCBodyPart.SwingAngleCap 20;
		MCBodyPart.SwingAmp SineWavePerSecond * 2.0;
		MCBodyPart.SwingAngleDir 1, 1, 0;
	}
	
	override void DoMotionAdjustments()
	{
		Super.DoMotionAdjustments();
		if (AngOff.Y < 35)	AngOff.Y = 35;
	}
}

//------------------------------------------------------------------------------
// Gibs
Class MCSpiderThoraxGib : MCBodyPartGib	
{
}
Class MCSpiderBackGib : MCBodyPartGib 
{
	Default
	{
		MCBodyPartGib.Offsets -1.2, 0, 0;
	}
}
Class MCSpiderHeadGib : MCBodyPartGib 
{
	Default
	{
		MCBodyPartGib.Offsets 0.8, 0, 0;
	}
}
Class MCSpiderLegGib : MCBodyPartGib 
{
	Default
	{
		MCBodyPartGib.EndPitch 0;
		MCBodyPartGib.Offsets 1.4, 0, 0;
	}
}