
// Contains info about who last damaged this actor. Used for DamageReact.
struct LastDamaged
{
	Actor i, s;
	int damage, dmg, flags;
	Name mod;
	double angle;
	bool used, secdmg;
}

//==============================================================================
//==============================================================================
// 
//==============================================================================
//==============================================================================

extend class TM_Monster
{
	//--------------------------------------------------------------------------
	// A virtual for monsters to adjust various aspects for AI difficulty setting.
	//--------------------------------------------------------------------------
	// private bool Inited, LookAllRound;
	virtual void AdjustDifficulty()
	{
		/*
		if (!Inited)
		{
			LookAllRound = irandom(0,1);
			Inited = true;
		}
		
		Switch (TMV_AIDifficulty)
		{
			Default:
			Case Dif_Easy:
				bLOOKALLAROUND = false;
				break;
			Case Dif_Normal:
				bLOOKALLAROUND = (LookAllRound);
				break;
			Case Dif_Hard:
				bLOOKALLAROUND = true;
				break;
		}
		*/
	}
	
	
	
	//--------------------------------------------------------------------------
	// TraceDistance
	//
	// Returns the distance to the edge of another actor.
	//--------------------------------------------------------------------------
	
	transient private TM_GDDTracer Trc;
	bool, double CheckTrace(Actor mo, double dist = 0, double zz = -1, bool flor = true, bool wall = true, bool ceil = true)
	{
		double dis = TraceDistance(mo, dist, zz);
		bool ret = (dis >= 0 && dis <= dist);
		return ret, dis;
	}
	double TraceDistance(Actor mo, double dist = 0, double zz = -1, bool flor = false, bool wall = true, bool ceil = false, bool solid = false)
	{
		if (dist <= 0 || !Targetable(mo))	return -1;
		
		// Set the position so its flat if the other's feet is above our
		// own, but not our head.
		if (zz < 0.0)
			zz = Clamp(	mo.pos.z, 
						Max(pos.z, floorz + 1, mo.pos.z), 
						Min(pos.z + height, ceilingz, mo.pos.z));
		
		Vector3 mpos = Vec2OffsetZ(0,0,zz),
				tpos = mo.pos;
		
		Vector3 APR = level.Vec3Diff(mpos, tpos);
		
		if (!Trc)	Trc = new('TM_GDDTracer');
		Trc.trg = mo;
		Trc.SetHittable(flor,wall,ceil);
		Trc.Trace(mpos, cursector, APR, dist + radius, 0);
		
		if (Trc.Results.HitType == TRACE_HitActor)
			return level.Vec3Diff(Trc.Results.HitPos, mpos).Length();
		return -1;
	}
	//--------------------------------------------------------------------------
	// "Respawns" the monster by clearing all inventory and effects, and other
	// stuff.
	
	virtual void PrepareRespawn()
	{
		if (health < 1 || bDORMANT)	return;
		
		Damagers.Clear();
		ClearTarget();
		Array<Powerup> Powers; Powers.Clear();
		Array<Inventory> Items; Items.Clear();
		for (let probe = inv; probe; probe = probe.inv)
		{
			if (probe)	Items.Push(probe);
		}
		
		for (int i = 0; i < Items.Size(); i++)
			if (Items[i])	Items[i].Destroy();
		
		// Mitigate any telefragging by making them pass through actors.
		bTHRUACTORS = true;
		A_Respawn(RSF_TELEFRAG);
		Deactivate(null);
	}
	
	void StoreHealthMul()
	{	if (bDORMANT)	HealthMul = Max(1.0, TMV_HealthMul);	}
	
	double GetHealthMul()
	{	return Max(1.0, HealthMul);	}
	
	protected void InitDrops(bool force = false)
	{	if (!Drops || force) Drops = TM_ItemDrops.Create();	}

	private int HPBonus, AmmoBonus;
	
	int GetHPArmorBonus()	{	return HPBonus;	}
	int GetAmmoBonus()		{	return AmmoBonus;	}
	
	// The goal of this function is simply to call Super.DropItems and pass in the
	// source, inflictor and damagetype for the monsters to use. The base class
	// simply checks if a player did the killing. Naturally this can be overridden
	// as needed, such as the creeper and skeleton special drops.
	virtual protected bool DropItems(Actor src, Actor inf, Name mod)
	{
		if (mod == 'Void')
			return false;
	
		bool good = (PlayerDamaged || (src is 'PlayerPawn'));
		/*
		if (!good)
		{
			for (int i = 0; i < Damagers.Size(); i++)
			{
				if (Damagers[i] is 'PlayerPawn')	
					good = true;	break;	
			}
			
		}
		*/
		if (good)
		{
			if (!Drops)	Drops = TM_ItemDrops.Create();
			if (ArmorInv)
			{
				[HPBonus, AmmoBonus] = ArmorInv.GetScore();
						
				if (HPBonus > 0)
				{
					Class<Inventory> ID;
					if (irandom(0,1) > 0)	ID = 'TM_PearlHealth';
					else					ID = 'TM_PearlArmor';
					
					Drops.SetAmount(ID, HPBonus, true);
				}
				if (AmmoBonus > 0)
					Drops.SetAmount('TM_PearlAmmo', AmmoBonus, true);
				
			}
			Drops.ResetIndex();
			while (Drops.Next())
			{
				DropItem(Drops.NextID, Drops.NextAmt);
				if (TMV_Debug == 21 && Drops.NextID)
					Console.Printf("%s", Drops.NextID.GetClassName());
			}
		}
		return good;
	}
	
	protected void DropItem(Class<Inventory> Cls, int Amount = -1, int Chance = 256)
	{
		GetEvent();
		if (Event)	Event.DropItem(self, Cls, Amount, Chance);
	}
	
	
	//--------------------------------------------------------------------------
	// Updates all things related to scale, including height and radius.
	protected void SetOverScale(double s)
	{
		OverScale = s;
		A_SetSize(Default.Radius * OverScale, Default.Height * OverScale);
		UpdateLimbs(Angs);
	}
	
	//--------------------------------------------------------------------------
	// Helper function simply to set limbs as well as 
	protected void SetTranslation(Name n)
	{
		A_SetTranslation(n);
		for (int i = 0; i < Limb.Size(); i++)
			if (Limb[i])	Limb[i].A_SetTranslation(n);
		PrevTrans = Translation;
	}
	
	
	
	
	
	
	
	bool CheckFear(Actor mo)
	{
		if (!mo)	
		{
			mo == Target;
			if (!mo)	return false;
		}
		if (CheckRelationTarget(mo) == Rel_Afraid)
			return true;
		
		return (!bNOFEAR && (bFRIGHTENED || mo.bFRIGHTENING));
	}
	
	virtual bool DoMissile()
	{
		return false;
	}
	
	
	
	virtual void SetMeleeAnimation()	{	}
	
	//==========================================================================
	// Goal Manipulation
	//==========================================================================
	// Currently heavily a WIP, needs reworking.
	enum EGoalStatus
	{
		GS_None = 0,
		GS_Chasing,
		GS_Reached
	};
	bool CheckGoal(bool istarget = false)
	{
		return ((Goal is "TM_Goal") && (!istarget || Target == Goal));
	}
	
	int CheckInGoalRange(double dist = 0.0, StateLabel next = 'Idle')
	{
		if (Goal && Target == Goal)
		{
			if (dist <= 0.0)	dist = radius;
			bool InZ = (pos.z <= Goal.pos.Z + Goal.Height) &&
						(pos.z >= Goal.pos.z);
			if (InZ && Distance2DSquared(Goal) <= (dist ** 2))
			{
				SetOrigin(Goal.pos, true);
				RemoveGoal();
				State s = ResolveState(next);
				if (s)	SetState(s);
				return GS_Reached;
			}
			return GS_Chasing;
		}
		return GS_None;
	}
	
	// Just in case something fucks with the goal pointer prematurely...
	private Actor BackupGoal; 
	virtual void SetGoal(Vector3 gp, double ang = 0, bool relative = false)
	{
	//	Console.Printf("SetGoal called");
		if (ValidateTarget())	return;
		
		if (!Goal)	
		{
			if (!BackupGoal)
			{
				Goal = Spawn('TM_Goal', gp);
			}
			else Goal = BackupGoal;
		}
		BackupGoal = Goal;
		
		if (relative)	Goal.Warp(Self, gp.x, gp.y, gp.z, ang, WARPF_NOCHECKPOSITION);
		else			Goal.SetOrigin(gp, false);
	}
	
	void RemoveGoal()
	{
		if (Goal is 'TM_Goal' && !Goal.bDESTROYED)
			Goal.Destroy();
		if (BackupGoal && !BackupGoal.bDESTROYED)
			BackupGoal.Destroy();
			
		Goal = BackupGoal = null;
	}
}

