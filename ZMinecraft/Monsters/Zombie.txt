//==============================================================================
// Zombie
// 
// Undead monsters that are surprisingly aware at knowing what to avoid... mostly.
// A common enemy that can spawn in packs.
//
// + Slow, lumbering
// + Weakness: Head
// + Burns in bright areas with a sky overhead
// - Has a chance to turn into a Skeleton if the head is exploited too much
// 		without killing it
// - Can call other zombies to fight with it, which can make more spawn.
//==============================================================================

/*

Minecraft/Zombie/Death
Minecraft/Zombie/Idle
Minecraft/Zombie/Pain
Minecraft/Zombie/Step

Head:		0.0,	0.0, 	4.8
(Gib) Body:	0.0,	0.0,	3.6
Arms:		0.0,	+-1.2,	4.4
Legs:		0.0,	+-0.4,	2.4

Gib Head: 	0.0,	0.0,	5.6
Gib Arms: 	0.0,	+-1.2,	3.6
Gib Leg:	0.0,	+-0.4,	1.2
*/

extend class MinecraftEnemy
{
	enum EAMFlags
	{
		AM_NOPARENT =	1,
		AM_FORCE =		1 << 1,
		
	//	AM_EXCLUDE =	1 << 1,
	};

	virtual void AlertMonsters(double dist, Actor targ = null, Class<Actor> filter = null, int limit = 0, int flags = 0)
	{
		if (dist <= 0.0)	return;
		if (!targ)
		{
			if (Target)	targ = Target;
			else		return;
		}
		
		if (targ.health < 1 || !targ.bSHOOTABLE ||
			targ.bNEVERTARGET || (!bFRIENDLY && targ.bNOTARGET))
			return;
		
		let it = BlockThingsIterator.Create(self, dist);
		Actor mo = null;
		int count = limit;
		while (it.Next())
		{
			mo = it.thing;
			if (!mo || mo.Health < 1)	continue;
			
			if (filter)
			{
				if (((flags & AM_NOPARENT) && mo.GetClass() != filter) ||
					(!(flags & AM_NOPARENT) && !(mo is filter)))
				{
					if (MCC_Debug)
					{
						Name cls = mo.GetClassName();
						name fil = filter.GetClassName();
						Console.Printf("%s doesn't fit filter %s, skipping", cls, fil);
					}
					continue;
				}
			}
			
			bool change = ((flags & AM_FORCE) || !mo.Target || mo.Target.Health < 1);
			if (!change)	continue;
			
			mo.bAMBUSH = true;
			mo.CopyFriendliness(self, change);
			
			let mon = MinecraftEnemy(mo);
			if (mon)	mon.Tracer = mo.Target;
			if (count > 0 && --count < 1)
				return;
			
		}
	}
}

Class MCZombie : MCUndead
{
	enum EAction
	{
		E_Idle,
		E_Wander,
		E_Chase
	};
	
	const _Radius = 1.6;
	const _Height = 6.4;
	const RealScale = 7.0;
	const RealRadius = RealScale * _Radius;
	const RealHeight = RealScale * _Height;
	
	private int CurrentAction;

	mixin NormalLimbCount;

	Default
	{
		
		MinecraftEnemy.PeacefulDespawn false; // Remove once testing is complete.
		MinecraftEnemy.Passive false;
		Scale RealScale;
		Radius RealRadius;
		Height RealHeight;
		Health 200;
		Speed 4;
		MeleeRange RealRadius + 8;
		
		ActiveSound "Minecraft/Zombie/Idle";
		PainSound "Minecraft/Zombie/Pain";
		DeathSound "Minecraft/Zombie/Death";
		MinecraftEnemy.StepSound "Minecraft/Zombie/Step";
		
		DamageFactor 0.92;
		MaxTargetRange 768; //In Minecraft it's 40 blocks (each block is 16). Here it's shorter.
		
		Tag "Zombie";
		Obituary "%o joined the 'breeeyynns' club, courtesy of a zombie's invitation.";
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		Init(LimbCount);
		Limb[Head] = MCBodyPart(Spawn('MCZombieHead', pos));
		Limb[Body] = MCBodyPart(Spawn('MCZombieBody', pos));
		Limb[ArmR] = MCBodyPart(Spawn('MCZombieArmRight', pos));
		Limb[ArmL] = MCBodyPart(Spawn('MCZombieArmLeft', pos));
		Limb[LegR] = MCBodyPart(Spawn('MCZombieLegRight', pos));
		Limb[LegL] = MCBodyPart(Spawn('MCZombieLegLeft', pos));
		for (int i = 0; i < Limb.Size(); i++)
		{
			if (Limb[i])
			{
				Limb[i].Master = self;
				Limb[i].Owner = self;
				Limb[i].Scale = Scale;
			}
		}
		
		Limb[Head].SetPosOffsets((0.0,	0.0, 4.8));
		Limb[Body].SetPosOffsets((0.0,  0.0, 3.6));
		Limb[ArmR].SetPosOffsets((0.0,  1.2, 4.4));
		Limb[ArmL].SetPosOffsets((0.0, -1.2, 4.4));
		Limb[LegR].SetPosOffsets((0.0,  0.4, 2.4));
		Limb[LegL].SetPosOffsets((0.0, -0.4, 2.4));
		
		Limb[ArmR].SetPitch(-90);
		Limb[ArmL].SetPitch(-90);
	}
	
	override void SetMeleeAnimation()
	{
		if (Limb[ArmR] && Limb[ArmL])
		{
			bool c1 = Limb[ArmR].SetAnimation('ZombieMelee');
			bool c2 = Limb[ArmL].SetAnimation('ZombieMelee');
		}
	}
	
	// When taking sector damage, try to get out of the sector by fleeing
	// towards the nearest mass of land, if without a target.
	// Otherwise if there is a target, 
	override void DamageReact(Actor inf, Actor src, int damage, int dmg, Name mod, int flags, double angle)
	{
		if (health < 1)	return;
		Super.DamageReact(inf, src, damage, dmg, mod, flags, angle);
		
		if (src && src.health > 1)
		{
			RemoveGoal();
			
			if (random[rng](1,10) == 1)
				AlertMonsters(MaxTargetRange * 2, target, GetClass(), random[rng](10,15), AM_NOPARENT);
		}
		/*
		else if (CurSector.DamageAmount > 0 && !Goal & !ValidateTarget())
		{
			int ang = random(0,359);
		
			for (double i = 0; i < 360; i += 15)
			{
				for (double j = 0; j < 1024; j += radius)
				{
					Vector3 p = pos + Vec3Angle(j, ang + i);
					Sector s = Level.PointInSector(p.xy);
					p.z = floorplane.ZatPoint(p.xy);
					if (!level.IsPointInLevel(p) || 
						s.DamageAmount > 0 ||
						!CheckPosition(p))	continue;
					
					SetGoal(p);
					return;
				}
			}
		}
		*/
	}
	
	override bool DropItems(Actor src, Actor inf, Name mod)
	{
		if (!Super.DropItems(src, inf, mod))
			return false;
		
		int end = 5;
		for (int i = 0; i < end; i++)
		{
			if (random[rng](1, end) == end)
			{
				Event.DropItem(self, 'HealthBonus');
				Event.DropItem(self, 'ArmorBonus');
			}
		}
		return true;
	}
	
	States
	{
	Spawn:
		TNT1 A 35;
	Idle:
		TNT1 A 0 A_Jump(256, random(1, 4));
		TNT1 AAAAAAAAAA 17 Look();
	IdleMove:
		TNT1 A 0 
		{
			LookTimer = 0;
			return A_Jump(256, random(1, 35*2));
		}
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 LookWander();
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 LookWander();
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 LookWander();
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 LookWander();
		Goto Idle;
	See:
		TNT1 A 1 Chase();
		Wait;
	Melee:
		TNT1 A 1 DoMelee(random[rng](20, 25));
		Goto See;
	}
}

//------------------------------------------------------------------------------
// Husks
// 
// - Doesn't burn in daylight
//------------------------------------------------------------------------------


//------------------------------------------------------------------------------
// Drowned
// 
// - Can swim
// - Can wield a trident
// - Doesn't burn in daylight
// - Immune to drowning
//------------------------------------------------------------------------------


//==============================================================================
//==============================================================================
// Limbs
//==============================================================================
//==============================================================================


Class MCZombieHead : MCBodyPartHead
{
	States
	{
	Spawn:
		ASDF A -1;
		Stop;
	}
}

Class MCZombieBody : MCBodyPartBody
{
}

Class MCZombieLegRight : MCBodyPartLeg
{
	Default
	{
		Radius 0.8;
		Height 2.0;
	}
}

Class MCZombieLegLeft : MCZombieLegRight
{
	Default
	{
		MCBodyPart.VelSineOffset 180;
	}
}

Class MCZombieArmRight : MCBodyPartArm
{
	Default
	{
		Radius 0.8;
		Height 2.0;
	}
}

Class MCZombieArmLeft : MCZombieArmRight
{
	Default
	{
		MCBodyPart.SineOffset 180, 180, 180;
	}
}

//------------------------------------------------------------------------------
// Gibs
//------------------------------------------------------------------------------

Class MCZombieHeadGib : MCBodyPartGib
{
	Default
	{
		MCBodyPartGib.Offsets 0, 0, 5.6;		//Head
	}
}

Class MCZombieArmRightGib : MCBodyPartGib
{
	Default
	{
		MCBodyPartGib.Offsets 0,  1.2, 3.6;		//Right Arm
	}
}

Class MCZombieArmLeftGib : MCBodyPartGib
{
	Default
	{
		MCBodyPartGib.Offsets 0, -1.2, 3.6;		//Left Arm
	}
}

Class MCZombieLegRightGib : MCBodyPartGib
{
	Default
	{
		MCBodyPartGib.Offsets 0,  0.4, 1.2;		//Right Leg
	}
}
Class MCZombieLegLeftGib : MCBodyPartGib
{
	Default
	{
		MCBodyPartGib.Offsets 0, -0.4, 1.2;		//Left Leg
	}
}

Class MCZombieBodyGib : MCBodyPartGib
{
	Default
	{
		MCBodyPartGib.Offsets 0, 0, 3.6;
	}
}

